   // Code your design here
module traffic_light_controller (
    input clk,
    input reset,
    input ped_button,

    output reg [2:0] vehicle_light,   // 001=GREEN, 010=YELLOW, 100=RED
    output reg ped_light               // 1=GREEN, 0=RED
);

    parameter S0 = 2'b00;
    parameter S1 = 2'b01;
    parameter S2 = 2'b10;
    parameter S3 = 2'b11;

    reg [1:0] current_state, next_state;

    parameter GREEN_TIME  = 10;
    parameter YELLOW_TIME = 5;
    parameter RED_TIME    = 5;
    parameter PED_TIME    = 8;

    reg [4:0] timer;
    reg ped_request;

    always @(posedge clk or posedge reset) begin
        if (reset)
            ped_request <= 0;
        else if (ped_button)
            ped_request <= 1;
        else if (current_state == S3)
            ped_request <= 0;
    end

    always @(posedge clk or posedge reset) begin
        if (reset) begin
            current_state <= S0;
            timer <= 0;
        end else begin
            current_state <= next_state;
            timer <= (current_state != next_state) ? 0 : timer + 1;
        end
    end

    always @(*) begin
        next_state = current_state;
        case (current_state)
            S0: if (timer == GREEN_TIME-1) next_state = S1;
            S1: if (timer == YELLOW_TIME-1) next_state = S2;
            S2: if (timer == RED_TIME-1)    next_state = (ped_request ? S3 : S0);
            S3: if (timer == PED_TIME-1)    next_state = S0;
        endcase
    end

    always @(*) begin
        case (current_state)
            S0: begin vehicle_light = 3'b001; ped_light = 0; end
            S1: begin vehicle_light = 3'b010; ped_light = 0; end
            S2: begin vehicle_light = 3'b100; ped_light = 0; end
            S3: begin vehicle_light = 3'b100; ped_light = 1; end
            default: begin vehicle_light = 3'b100; ped_light = 0; end
        endcase
    end

endmodule

